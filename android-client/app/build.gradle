apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.protobuf'
apply plugin: 'com.google.osdetector'

android {
    compileSdkVersion 30

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    defaultConfig {
        applicationId 'io.grpc.demo'
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName '1.0'
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled true
            proguardFiles 'proguard-android.txt', 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            proto {
                srcDirs '../../proto'
            }
        }
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning', 'HardcodedText', 'SetTextI18n', 'DuplicateIncludedIds'
        textReport true
        textOutput 'stdout'
    }

    androidExtensions {
        experimental = true
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }
}

protobuf {
    protoc { artifact = 'com.google.protobuf:protoc:3.12.0' }
    plugins {
        javalite { artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0' }
        grpc { artifact = 'io.grpc:protoc-gen-grpc-java:1.31.1' }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins { java { option 'lite' } }
            task.plugins { grpc { option 'lite' } }
        }
    }
}

dependencies {
    testImplementation 'junit:junit:4.13.1'
    testImplementation 'org.conscrypt:conscrypt-openjdk:2.2.1:' + osdetector.classifier

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.activity:activity-ktx:1.1.0'
    implementation 'androidx.fragment:fragment-ktx:1.3.0-beta02'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-savedstate:2.3.0-beta01'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.2'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.2'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.security:security-crypto:1.1.0-alpha03'
    implementation 'com.google.android.material:material:1.3.0-alpha04'

    implementation 'io.grpc:grpc-okhttp:1.34.0'
    implementation 'io.grpc:grpc-protobuf-lite:1.34.0'
    implementation 'io.grpc:grpc-stub:1.34.0'

    implementation 'org.apache.tomcat:annotations-api:6.0.53'

    implementation 'io.reactivex.rxjava2:rxjava:2.2.20'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'com.github.ReactiveX:RxKotlin:2.4.0'
    implementation 'com.jakewharton.rxrelay2:rxrelay:2.1.1'

    implementation 'org.slf4j:slf4j-api:1.7.30'
    implementation 'com.github.tony19:logback-android:2.0.0'
}
